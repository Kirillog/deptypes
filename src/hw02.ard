\import Data.Bool
\import Data.Or
\import Function.Meta
\import Logic
\import hw01 (fac)
\import lect02 (List, cons, nil)

-- 1. Определите полиморфный tail для списков
\func tail {A : \Type} (xs : List A) : List A \elim xs
    | nil => nil
    | cons x xs => xs

-- 2. Реализуйте любой (but choose wisely) алгоритм сортировки. Функцию сравнения можно принимать в качестве аргумента

\func sort {A : \Type} (xs : List A) (leq : A -> A -> Bool) : List A \elim xs
    | nil => nil
    | cons x xs => insert x (sort xs leq)
    \where {
        \func insert (x : A) (xs : List A) : List A \elim xs
            | nil => cons x nil
            | cons a xs \as t => \case leq x a \with {
                | Bool.true => cons x t
                | Bool.false => cons a (insert x xs)
            }
    }

\func leqNat (n m : Nat) : Bool
    | Nat.zero, m => Bool.true
    | Nat.suc n', m => \case m \with {
        | Nat.zero => Bool.false
        | Nat.suc m' => leqNat n' m'
    }

\func sortTest1 : sort (cons 2 $ cons 1 nil) leqNat = (cons 1 $ cons 2 nil) => idp
\func sortTest2 : sort (cons 2 $ cons 3 $ cons 1 nil) leqNat = (cons 1 $ cons 2 $ cons 3 nil) => idp


-- 3. Докажите ассоциативность && и ||

\func &&-assoc (x y z : Bool) : (x and y) and z = x and (y and z) \elim x
    | false => idp
    | true => idp

\func ||-assoc (x y z : Bool) : (x or y) or z = x or (y or z) \elim x
    | false => idp
    | true => idp

-- 4. Докажите, что fac 3 равно 6

\func fac3eq6 : fac 3 = 6 => idp

-- 5. Докажите следующие пропозициональные тавтологии (можно решить любые 2 из 3 задач)

-- t1 : (P and Q -> R) -> P -> Q -> R
\func t1 {P Q R : \Type} (f : \Sigma P Q -> R) (p : P) (q : Q) : R => f (p, q)

-- t2 : (P -> R) -> (Q -> R) -> P or Q -> R
\func t2 {P Q R : \Type} (f : P -> R) (g : Q -> R) (h : Or P Q) : R \elim h
    | inl a => f a
    | inr b => g b

-- t3 : ((P -> Q -> R) -> P) -> (P -> R) -> R
\func t3 {P Q R : \Type} (f : (P -> Q -> R) -> P) (g : P -> R) : R => g (f (\lam p q => g p))

-- 6. Сформулируйте и докажите следующие пропозициональные тавтологии (можно решить любые 2 из 3 задач)

-- t4 : (P -> Q -> R) -> P /\ Q -> R
\func t4 {P Q R : \Type} (f : P -> Q -> R) (p : P) (q : Q) : R => f p q

-- t5 : (P or Q -> P /\ Q) -> (P -> Q) /\ (Q -> P)
\func t5 {P Q : \Type} (f : Or P Q -> \Sigma P Q) : \Sigma (P -> Q) (Q -> P) => (\lam p => (f (inl p)).2, \lam q => (f (inr q)).1)

-- t6 : ((((P -> Q) -> P) -> P) -> Q) -> Q
\func t6 {P Q : \Type} (f : (((P -> Q) -> P) -> P) -> Q) : Q => f (\lam g => g (\lam h => f (\lam _x => h)))

-- 7. Докажите следующие утверждения

-- s1 : Если для любого x : Nat верно P(x), то существует x : Nat для которого P(x) верно
\func s1 (P : Nat -> \Type) (h : \Pi (x : Nat) -> P x) : \Sigma (x : Nat) (P x) => (zero, h zero)

-- s2 : Если не существует x : Nat такого, что верно P(x), то P(3) не верно
\func s2 (P : Nat -> \Type) (h : \Sigma (x : Nat) (P x) -> Empty) : P 3 -> Empty => \lam p => h (3, p)

-- 8. Сформулируйте и докажите следующие утверждения

-- s3 : Если для всех x : Nat верно, что P(x) влечет Q(x), и существует x : Nat для которого верно P(x),
-- то существует x : Nat для которого верно Q(x)
\func s3 (P Q : Nat -> \Type) (h : \Pi (x : Nat) -> P x -> Q x) (g : \Sigma (x : Nat) (P x)) : \Sigma (x : Nat) (Q x) =>
    (g.1, h g.1 g.2)

-- s4 : Если для всех x : Nat либо не верно P(x), либо не верно Q(x), то P(3) влечет, что Q(3) не верно
\func s4 (P Q : Nat -> \Type) (h : \Pi (x : Nat) -> Or (P x -> Empty) (Q x -> Empty)) : P 3 -> Q 3 -> Empty =>
    \lam p q => \case h 3  \with {
        | inl a => a p
        | inr b => b q
    }