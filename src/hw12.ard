\import Data.List
\import Function ()
\import Function.Meta
\import Logic
\import Meta
\import Paths.Meta
\import hw07 (In)
\import lect12 \hiding (Int', pos, neg)

-- 1. Определите функцию negPred : Int -> Int такую, что negPred x = x, если x > 0, и negPred x = x - 1, если x <= 0

\func negPred (x : Int) : Int \elim x
  | pos (suc x) => pos (suc x)
  | pos 0 => -1
  | neg n => neg (suc n)

-- 2. Определите функции сложения и умножения для Int

\func \infixl 6 + (x y : Int) : Int \elim x, y
  | pos 0, y => y
  | pos x, pos y => pos (x Nat.+ y)
  | pos (suc x), neg (suc y) => pos x + neg y
  | neg (suc x), pos 0 => neg (suc x)
  | neg (suc x), pos (suc y) => neg x + pos y
  | neg (suc x), neg (suc y) => neg (suc x Nat.+ suc y)

\func \infixl 7 * (x y : Int) : Int \elim x, y
  | pos n, pos m => n Nat.* m
  | pos n, neg (suc m) => neg (n Nat.* suc m)
  | neg (suc n), pos m => neg (m Nat.* suc n)
  | neg (suc n), neg (suc m) => suc n Nat.* suc m

-- 3. Определите тип данных BinNat для бинарных натуральных чисел
--    У него должно быть три конструктора: для 0, для чисел вида 2*n и чисел вида 2*n+1
--    Такой тип данных содержит несколько различных представлений для нуля
--    Используйте типы данных с условиями, чтобы вычислительно отождествить различные нули

\data BinNat
  | zero
  | o BinNat
  | z BinNat \with {
    | zero => zero
  }

-- 4. Докажите, что функция pred сюръективна

\func pred (n : Nat) : Nat
  | 0 => 0
  | suc n => n

\func pred-is-sur : isSur pred => \lam b => inP (suc b, idp)

-- 5. Докажите, что функция suc не сюръективна

\func suc-is-not-sur (p : isSur suc) : Empty => \case p 0 \with {
  | inP x => \case x.2
}

-- 6. Пусть f : A -> B и g : B -> C ─ некоторые функции
--    Докажите, что если f и g сюръективны, то g o f также сюръективна
--    Докажите, что если g o f сюръективна, то g также сюръективна

\func o-sur {A B C : \Type} (f : A -> B) (g : B -> C) (p : isSur f) (q : isSur g) : isSur (g Function.o f) =>
  \lam b => \case q b \with {
    | inP (b, q1) => \case p b \with {
      | inP (a, p1) => inP (a, unfold (Function.o) $ rewrite p1 q1)
    }
  }

\func o-sur' {A B C : \Type} (f : A -> B) (g : B -> C) (p : isSur (g Function.o f)) : isSur g =>
  \lam b => \case p b \with {
    | inP (a, p1) => inP (f a, p1)
  }

-- 7. Определите множество конечных подмножеств множества A,
--    то есть списков элементов A с точностью до перестановок и повторений элементов

\func Set (A : \Set) : \Set
  => Quotient (List A) (\lam xs ys => \Pi (x : A) -> (TruncP $ In x xs) <-> (TruncP $ In x ys))

-- 8. Докажите теорему Кантора

-- Теорема Кантора говорит, что для любого множества A мощность множества его подмножеств строго больше, чем мощность A

-- Множество подмножеств можно определить следующим образом:

\func Subs (A : \Set) => A -> \Prop

-- Формально утверждение теоремы Кантора состоит из двух частей:
-- "существует инъекция из A в Subs A" и "не существует сюръекции из A в Subs A"

\func cantor1 (A : \Set) : \Sigma (f : A -> Subs A) (isInj f)
  => (=, =-isInj)

\func cantor2 (A : \Set) (f : A -> Subs A) (p : isSur f) : Empty
  => {?}

-- Optional
-- 9. Постройте функции Nat -> BinNat и BinNat -> Nat и докажите, что они взаимно обратны

\func NatToBinNat (n : Nat) : BinNat => helper n n
\where {
  \func helper (f n : Nat) : BinNat
    | 0, _ => zero
    | suc f, 0 => zero
    | suc f, suc n1 => \case Nat.divMod n 2 \with {
      | (n2, 0) => z (helper f n2)
      | (n2, 1) => o (helper f n2)
    }
}

\func BinNatToNat (b : BinNat) : Nat \elim b
  | zero => 0
  | o b => suc $ BinNatToNat b Nat.* 2
  | z b => BinNatToNat b Nat.* 2

\func nbn (n : Nat) : BinNatToNat (NatToBinNat n) = n => {?}

\func bnb (b : BinNat) : NatToBinNat (BinNatToNat b) = b => {?}
